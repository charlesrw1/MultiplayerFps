

#ifdef DEPTHONLY
void main()
{
}

#else

layout(location = 0) out vec4 FS_OUT_Color;

layout(binding = 0) uniform sampler2D GBUFFER0;		// ws normal (xyz)
layout(binding = 1) uniform sampler2D GBUFFER1;		// albedo, ao
layout(binding = 2) uniform sampler2D GBUFFER2;		// metallic,roughness, custom, matid
layout(binding = 3) uniform sampler2D DEPTH;		// depth texture (writes disabled)


layout(binding = 4) uniform sampler2D SSAOBlurred;

layout(binding = 5) uniform samplerCube Skylight;
layout(binding = 6) uniform sampler2D PBR_brdflut;

#include "SharedGpuTypes.txt"
#include "LightingShared.txt"

layout (binding = 0, std140) uniform Ubo_View_Constant_Buffer {
	Ubo_View_Constants_Struct g;
};

uniform float specular_ao_intensity = 3.0;

float linearize_depth(float d,float zNear)
{
    return zNear/ d;
}

vec3 WorldPosFromDepth(vec2 TexCoord, float depth) {

	// 7/30: reverse-Z change
    float z = depth;// * 2.0 - 1.0;

    vec4 clipSpacePosition = vec4(TexCoord * 2.0 - 1.0, z, 1.0);
    vec4 viewSpacePosition = g.inv_viewproj * clipSpacePosition;

    // Perspective division
    viewSpacePosition /= viewSpacePosition.w;

    return viewSpacePosition.xyz;
}

#define PI 3.1415927
// ----------------------------------------------------------------------------
float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness*roughness;
    float a2 = a*a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH*NdotH;

    float nom   = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / max(denom,0.0000001);
}
// ----------------------------------------------------------------------------
float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r*r) / 8.0;

    float nom   = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}
// ----------------------------------------------------------------------------
float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);
    float ggx1 = GeometrySchlickGGX(NdotL, roughness);

    return ggx1 * ggx2;
}
// ----------------------------------------------------------------------------
vec3 fresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}
vec3 modified_f_schlick(vec3 F0, float F90, float cosTheta)
{
	return F0 + (vec3(F90) - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

vec3 FresnelSchlickRougness(float cosTheta, vec3 F0, float roughness)
{
	 return F0 + (max(vec3(1.0 - roughness), F0) - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

float compute_specular_occlusion(float ao, float NdotV, float roughness)
{
    float out_val = (pow(NdotV + ao, exp2(-16.0 * roughness - 1.0)) - 1.0 + ao);
	return clamp(out_val,0,1);
}


vec3 pbr_calculate_reflection(vec3 worldspace, vec3 Albedo, float Roughness, float Metalness, vec3 Normal, float ambient_occlusion)
{
	vec3 view_pos = g.viewpos_time.xyz;
	vec3 view_front = g.viewfront.xyz;

	vec3 V = normalize(view_pos-worldspace);
	vec3 R = reflect(-V,Normal);
	
	vec3 F0 = vec3(0.04);
	F0 = mix(F0,Albedo,Metalness);
	
	const float MAX_REFLECTION_LOD = 7.0;
	vec3 prefiltered_color = textureLod(Skylight,vec3(R), Roughness*MAX_REFLECTION_LOD).rgb;

	float NdotV = max(dot(Normal,V),0.0);
	vec3 F = FresnelSchlickRougness(NdotV,F0,Roughness);
	//float F90 = clamp(50*dot(F0,vec3(0.33)),0,1);
	//vec3 F = modified_f_schlick(F0,F90,NdotV);
	
	vec2 env_brdf = texture(PBR_brdflut,vec2(max(dot(Normal,V),0.0),Roughness)).rg;
	
	vec3 specular = prefiltered_color*(F*env_brdf.x+env_brdf.y);
	float spec_occlusion =compute_specular_occlusion(ambient_occlusion,NdotV,Roughness);
	

	return specular*spec_occlusion;
}
void main()
{
	vec2 uv = vec2(gl_FragCoord.xy) / vec2(g.viewport_size.xy);
	//
	vec4 metal_rough = texture(GBUFFER2,uv);
	vec4 albedo = texture(GBUFFER1,uv);
	vec4 depth_tex = texture(DEPTH,uv);
	vec4 normal_t = texture(GBUFFER0,uv);
	float ao = texture(SSAOBlurred,uv).r;
	
	uint type = get_gbuffer_material_type(metal_rough);
	if(type==MATERIAL_TYPE_UNLIT)
		discard;
	
	float depth_value = depth_tex.r;
	if(linearize_depth(depth_value,g.near) > 3000.0)
		discard;
	
	vec3 worldspace = WorldPosFromDepth(uv, depth_value);
	
	// intensifying the ao here. i guess this isnt "right". but otherwise specular occlusion is too weak in my opinion
	// intensifying ao for indirect diffuse gets ugly halo stuff, but just for specular i think looks alright
	vec3 out_color = pbr_calculate_reflection(worldspace, albedo.xyz, metal_rough.y, metal_rough.x, normal_t.xyz,pow(ao,specular_ao_intensity));
	
	FS_OUT_Color = vec4(out_color,1.0);
}
#endif 